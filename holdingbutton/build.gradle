apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

ext {
    artifactVersion = '0.1.0'
    artifactName = 'holdingbutton'
    siteUrl = 'https://github.com/dewarder/HoldingButton'
    gitUrl = 'https://github.com/dewarder/HoldingButton.git'
}

version = artifactVersion
group = 'com.dewarder'

android {
    compileSdkVersion 26
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName artifactVersion
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-annotations:25.3.1'
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name artifactName
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'dewarder'
                        name 'Artem Glugovsky'
                        email 'dewarder@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task classesJar(type: Jar) {
    from "$buildDir/intermediates/classes/release"
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties['bintray.user']
    key = properties['bintray.apikey']
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = "$group:$artifactName"
        userOrg = 'dewarder'
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        version {
            name = artifactVersion
            desc = 'Add RTL support. Upgrade dependencies versions.'
            released = new Date()
            vcsTag = "$artifactVersion"
        }
    }
}